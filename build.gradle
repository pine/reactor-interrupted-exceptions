// ----- Build script -------------------------------------

buildscript {
    ext {
        junitVersion = '5.4.2'
        reactorVersion = '3.3.0.RELEASE'
    }
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.11'
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
    }
}


// ----- Project ------------------------------------------

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

group 'moe.pine'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    jcenter()
}

dependencies {
    implementation "io.projectreactor:reactor-core:$reactorVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}


// ----- Build Time Tracker -------------------------------

apply plugin: 'build-time-tracker'

buildtimetracker {
    reporters {
        summary {
            ordered true
            barstyle 'ascii'
        }
    }
}


// ----- SpotBugs -----------------------------------------

apply plugin: 'com.github.spotbugs'

spotbugs {
    toolVersion = '3.1.8'
    excludeFilter = file("${project.rootDir}/findbugs-exclude.xml")
    spotbugsTest.enabled = false
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}


// ----- PMD ----------------------------------------------

apply plugin: 'pmd'

pmd {
    toolVersion = '6.20.0'
}
