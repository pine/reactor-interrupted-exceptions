// ----- Build script -------------------------------------

buildscript {
    ext {
        // Project details
        version = '0.1.0'
        description = 'Reactor InterruptedException utilities.'
        group = 'moe.pine'
        websiteUrl = 'https://github.com/pine/reactor-interrupted-exceptions'
        issueTrackerUrl = 'https://github.com/pine/reactor-interrupted-exceptions/issues'
        gitUrl = 'https://github.com/pine/reactor-interrupted-exceptions.git'
        userOrg = 'pinemz'

        // Dependencies
        junitVersion = '4.12'
        mockitoVersion = '2.24.5'
        powermockVersion = '2.0.0'
        reactorVersion = '3.3.0.RELEASE'
    }
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.11'
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
    }
}


// ----- Project ------------------------------------------

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

group 'moe.pine'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    jcenter()
}

dependencies {
    implementation "io.projectreactor:reactor-core:$reactorVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.powermock:powermock-core:$powermockVersion"
    testImplementation "org.powermock:powermock-reflect:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4-rule:$powermockVersion"
    testImplementation "org.powermock:powermock-api-mockito2:$powermockVersion"
}


// ----- Build Time Tracker -------------------------------

apply plugin: 'build-time-tracker'

buildtimetracker {
    reporters {
        summary {
            ordered true
            barstyle 'ascii'
        }
    }
}


// ----- SpotBugs -----------------------------------------

apply plugin: 'com.github.spotbugs'

spotbugs {
    toolVersion = '3.1.8'
    excludeFilter = file("${project.rootDir}/findbugs-exclude.xml")
    spotbugsTest.enabled = false
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}


// ----- PMD ----------------------------------------------

apply plugin: 'pmd'

pmd {
    toolVersion = '6.20.0'
}


// ----- Bintray ------------------------------------------

apply from: "${rootProject.projectDir}/gradle/bintray.gradle"
